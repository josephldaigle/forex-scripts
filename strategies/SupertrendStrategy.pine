//@version=5
strategy('Supertrend Strategy - PineConnector', overlay=true)

// variables 
LicenseID = 5997022937301 // 1. change to your PineConnector License ID (required) 

atrPeriod = input(10, 'ATR Length')
atrMultiple = input.float(1.5, 'ATR Multiple', step=0.5)
factor = input(3, 'Factor')
riskvalue = input.int(1, 'Risk Value')

maChoice = input.string("SMA", "Moving Average", options=["EMA", "SMA"])
maPeriod = input.int(20, "Moving Average Period")

// compute supertrend
[supertrend, direction] = ta.supertrend(factor, atrPeriod)

// compute moving average
ma = (maChoice == "SMA") ? ta.sma(close, maPeriod) : ta.ema(close, maPeriod)
plot(ma, "Moving Average", color=color.new(color.orange, 0), style=plot.style_linebr)

// compute atr
atr = ta.atr(atrPeriod)

// plot supertrend
bodyMiddle = plot((open + close) / 2, display=display.none)
upTrend = plot(direction < 0 ? supertrend : na, 'Up Trend', color=color.new(color.green, 0), style=plot.style_linebr)
downTrend = plot(direction < 0 ? na : supertrend, 'Down Trend', color=color.new(color.red, 0), style=plot.style_linebr)

fill(bodyMiddle, upTrend, color.new(color.green, 90), fillgaps=false)
fill(bodyMiddle, downTrend, color.new(color.red, 90), fillgaps=false)

// place trade orders
if ta.change(direction) < 0
    strategy.entry('My Long Entry Id', strategy.long)
    alert(str.tostring(LicenseID)+',buy,' + syminfo.ticker + ',sl=' + str.tostring(atr * atrMultiple) + ',risk=' + str.tostring(riskvalue), alert.freq_once_per_bar_close)

if ta.change(direction) > 0
    strategy.entry('My Short Entry Id', strategy.short)
    alert(str.tostring(LicenseID)+',sell,' + syminfo.ticker + ',sl=' + str.tostring(atr * atrMultiple) + ',risk=' + str.tostring(riskvalue), alert.freq_once_per_bar_close)

// plot labels
plotshape(ta.change(direction) < 0, style=shape.labelup, location=location.belowbar, color=color.new(#046ff9, 0), size=size.large, text='PineConnector \n Buy', textcolor=color.new(color.white, 0))
plotshape(ta.change(direction) > 0, style=shape.labeldown, location=location.abovebar, color=color.new(#046ff9, 0), size=size.large, text='PineConnector \n Sell', textcolor=color.new(color.white, 0))